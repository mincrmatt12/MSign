// Simple HTTP client parser

out int result_code = 0;
out int content_length;
out enum{CLOSE,OTHER} connection;

macro ows {
	optional { " "; }
}

parser {
	connection = OTHER;
	"HTTP";
	wait " ";

	// Get the response code
	loop number_parser {
		case {
			" " -> {break number_parser;}
			/\d/ -> {
				result_code = [result_code * 10 + ($last - 48)]; // '0' = 48
			}
		}
	}

	// Wait for the newline
	wait "\r\n";

	// Try and find a response header
	loop {
		case {
			"\r\n" -> {finish;}

			"Content-Length:"i -> {
				ows();

				content_length = 0;
				// Read in a nice hearty integer
				loop number_parser {
					case {
						"\r\n" -> {break number_parser;}
						/\d/ -> {
							content_length = [content_length * 10 + ($last - 48)]; // '0' = 48
						}
					}
				}
			}
			"Connection:"i -> {
				ows();

				try {
					"close";
					connection = CLOSE;
					wait "\r\n";
				}
				catch (nomatch) {
					connection = OTHER;
					wait "\r\n";
				}
			}
			else -> {wait "\r\n";}
		}
	}
}
