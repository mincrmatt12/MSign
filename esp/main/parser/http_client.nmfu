// Simple HTTP client parser

out int result_code = 0;
out int content_length;
out enum{CLOSE,OTHER} connection;

macro ows() {
    optional { " "; }
}

macro read_number(out target, match delimit) {
    target = 0;
    foreach {
        /\d+/;
    } do {
        target = [target * 10 + ($last - '0')];
    }

    delimit;
}

parser {
    connection = OTHER;
    /.?\s*HTTP/;
    wait " ";

    // Get the response code
    read_number(result_code, " ");

    // Wait for the newline
    wait "\r\n";

    // Try and find a response header
    loop {
        case {
            "\r\n" -> {finish;}

            "Content-Length:"i -> {
                ows();

                read_number(content_length, "\r\n");
            }
            "Connection:"i -> {
                ows();

                try {
                    "close";
                    connection = CLOSE;
                }
                catch (nomatch) {
                    connection = OTHER;
                }

                wait "\r\n";
            }
            else -> {wait "\r\n";}
        }
    }
}
